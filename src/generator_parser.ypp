%{

#include <stdio.h>
#include <math.h>
#include <stdexcept>
#include <sstream>
#include <memory>

#define YY_DECL int yylex( YYSTYPE * yylval, yyscan_t yyscanner, Parser * ptr )
#define YYLEX_PARAM yyscanner, ptr

#include "generator.h"
#include "generator_scanner.h"
#include "generator_parser.hpp"

YY_DECL;


extern int yyerror(yyscan_t yyscanner, Parser * ptr, const char *s);

using namespace std;

%}
%union {
	const char * str;
	double num;
	struct Expression * exp;
}
%parse-param { yyscan_t yyscanner }
%parse-param { Parser * ptr }
%define api.pure
%token NAME
%token PRECISSION
%token METHOD
%token SURFACE
%token APPROXIMATION
%token START
%token DECLARE
%token SCALAR
%token FUNCTION
%token EQUATION
%token JACOBIAN
%token DELTA
%token RARROW
%token LARROW
%token <str> VAR
%token <num> NUMBER
%type  <exp> expr;
%type  <exp> sexpr;
%type  <exp> operator;
%right '='
%left '+' '-'
%left '*' '/'
%left ','
%right '^'
%%

body: '\n'
	| clause '\n'
	| body '\n'
	| body clause '\n'
	;

clause: 
	NAME ':' VAR { ptr->set_name($3); }
	| PRECISSION ':' VAR { ptr->set_precission($3); }
	| METHOD ':' VAR { ptr->set_method($3); }
	| SURFACE ':' VAR { ptr->set_surface($3); }
	| APPROXIMATION ':' VAR	{ ptr->set_approximation($3); }
	| START ':' VAR { ptr->add_initial($3); }
	| DECLARE ':' VAR '=' expr { ptr->add_declaration($3, $5); }
	| DECLARE ':' VAR LARROW expr { /* ptr->add_dependency($3, $5); */ }
	| SCALAR ':' VAR { ptr->add_scalar($3); }
	| FUNCTION ':' VAR '(' VAR ',' VAR ')' 	{ ptr->add_function($3, 2); }
	| FUNCTION ':' VAR '(' VAR ',' VAR ',' VAR ')' { ptr->add_function($3, 3); }
	| EQUATION ':' expr '=' expr { ptr->add_equation($3, $5); }
	;

expr:
	VAR 
		{
			$$    = ptr->new_expression();
			ptr->check_var($1);
			*($$) = $1;
			fprintf(stderr, "%s\n", $1);
		}
	| NUMBER 
		{
			$$    = ptr->new_expression();
			(*$$) = $1;
		}
	| VAR '[' NUMBER ']' 
		{
			$$    = ptr->new_expression();
			ptr->check_var($1);
			*($$) = $1;
		}
	| '(' expr ')' { $$ = $2; }
	| expr '^' expr { (*$1) ^= *($3); $$ = $1; }
	| expr '+' expr { (*$1) += *($3); $$ = $1; }
	| expr '-' expr { (*$1) -= *($3); $$ = $1; }
	| expr '/' expr { (*$1) /= *($3); $$ = $1; }
	| expr '*' expr { (*$1) *= *($3); $$ = $1; }
	| operator '(' sexpr ')' 
		{
			(*$1)(*$3); $$ = $1;
		}
	;

sexpr: expr { $$ = $1; }
	| sexpr ',' expr { (*$1) , (*$3); $$ = $1; }
	;
	
operator: JACOBIAN { $$ = ptr->new_expression(); *$$ = "Jacobian"; }
	| DELTA { $$ = ptr->new_expression(); *$$ = "Delta"; }
	;
%%
int yyerror(yyscan_t yyscanner, Parser * ptr, const char * s)
{
	std::stringstream str;
	str << "syntax error near line " << yyget_lineno(yyscanner);
	throw std::logic_error(str.str());
	return 0;
}

int yylex( YYSTYPE &, yyscan_t yyscanner, Parser * ptr );

int yywrap( yyscan_t yyscanner )
{
	return 1;
}

void Parser::parse(FILE * f)
{
	int status;
	YYSTYPE val;
	yyscan_t scanner;
	yylex_init( &scanner );
	yyset_in(f, scanner);
	try {
		while (yyparse(scanner, this));
	} catch ( exception & e) {
		yylex_destroy(scanner);
		throw;
	}
	yylex_destroy (scanner);
}

