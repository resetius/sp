%{

#include <stdio.h>
#include <math.h>
#include <stdexcept>
#include <sstream>
#include <memory>

#define YY_DECL int yylex( yyscan_t yyscanner, YYSTYPE & yylval )

#include "generator_scanner.h"
#include "generator.h"

extern int yyerror(Parser * ptr, yyscan_t yyscanner, const char *s);

using namespace std;

%}
%union {
	char * str;
	double num;
	struct Expression * exp;
}
%parse-param { Parser * ptr }
%parse-param { yyscan_t yyscanner }
%define api.pure
%define api.push_pull "push"
%token NAME
%token PRECISSION
%token METHOD
%token SURFACE
%token APPROXIMATION
%token START
%token DECLARE
%token SCALAR
%token FUNCTION
%token EQUATION
%token JACOBIAN
%token DELTA
%token ARROW
%token <str> VAR
%token <num> NUMBER
%type  <exp> expr;
%type  <exp> sexpr;
%type  <exp> operator;
%right '='
%left '+' '-'
%left '*' '/'
%left ','
%right '^'
%destructor { fprintf(stderr, "destroy %s\n", $$);  } <>
%%

body: '\n'
	| clause '\n'
	| body '\n'
	| body clause '\n'
	;

clause: 
	NAME ':' VAR { ptr->set_name($3); }
	| PRECISSION ':' VAR { ptr->set_precission($3); }
	| METHOD ':' VAR { ptr->set_method($3); }
	| SURFACE ':' VAR { ptr->set_surface($3); }
	| APPROXIMATION ':' VAR	{ ptr->set_approximation($3); }
	| START ':' VAR { ptr->add_initial($3); }
	| DECLARE ':' VAR '=' expr { ptr->add_declaration($3, $5); }
	| SCALAR ':' VAR { ptr->add_scalar($3); }
	| FUNCTION ':' VAR '(' VAR ',' VAR ')' 	{ ptr->add_function($3, 2); }
	| FUNCTION ':' VAR '(' VAR ',' VAR ',' VAR ')' { ptr->add_function($3, 3); }
	| EQUATION ':' expr '=' expr { ptr->add_equation($3, $5); }
	;

expr:
	VAR 
		{
			$$    = ptr->new_expression();
			ptr->check_var($1);
			*($$) = $1;
			fprintf(stderr, "%s\n", $1); free($1);
		}
	| NUMBER 
		{
			$$    = ptr->new_expression();
			(*$$) = $1;
			fprintf(stderr, "%lf\n", $1); 
		}
	| VAR '[' NUMBER ']' 
		{
			$$    = ptr->new_expression();
			ptr->check_var($1);
			*($$) = $1;
			fprintf(stderr, "%s\n", $1); fprintf(stderr, "%d\n", $3); free($1); 
		}
	| '(' expr ')' { $$ = $2; }
	| expr '^' expr { (*$1) ^= *($3); $$ = $1; }
	| expr '+' expr { (*$1) += *($3); $$ = $1; }
	| expr '-' expr { (*$1) -= *($3); $$ = $1; }
	| expr '/' expr { (*$1) /= *($3); $$ = $1; }
	| expr '*' expr { (*$1) *= *($3); $$ = $1; }
	| operator '(' sexpr ')' 
		{
			(*$1)(*$3); $$ = $1;
		}
	;

sexpr: expr { $$ = $1; }
	| sexpr ',' expr { (*$1) , (*$3); $$ = $1; }
	;
	
operator: JACOBIAN { $$ = ptr->new_expression(); *$$ = "Jacobian"; }
	| DELTA { $$ = ptr->new_expression(); *$$ = "Delta"; }
	;
%%
int yyerror(Parser * ptr, yyscan_t yyscanner, const char * s)
{
	std::stringstream str;
	str << "syntax error near line " << yyget_lineno(yyscanner);
	throw std::logic_error(str.str());
	return 0;
}

int yylex( yyscan_t yyscanner, YYSTYPE & );

int yywrap( yyscan_t yyscanner )
{
	return 1;
}

void Parser::parse(FILE * f)
{
	int yychar;
	int status;
	yyscan_t scanner;
	yylex_init( &scanner );
	yyset_in(f, scanner);
	yypstate *ps = yypstate_new ();
	try {
		do {
			YYSTYPE val;
			yychar = yylex (scanner, val);
			status = yypush_parse (ps, yychar, &val, this, scanner);
		} while (status == YYPUSH_MORE);
	} catch ( exception & e) {
		yypstate_delete (ps);
		yylex_destroy(scanner);
		throw;
	}
	yypstate_delete (ps);
	yylex_destroy (scanner);
}

